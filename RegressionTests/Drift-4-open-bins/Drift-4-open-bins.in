/*      Drift-4-open-bins.in
	OPALX
    Drift + constant E cavity: bunch with longitudinal extent enters cavity.
    - Start: few bins (all similar energy in drift)
    - Middle: many bins (head accelerating, tail still in drift -> large energy spread)
    - End: few bins (most at ~c)
*/

OPTION, PSDUMPFREQ      = 10;     // 6d data written every 10th time step (h5).
OPTION, STATDUMPFREQ    = 1;     // Beam Stats written every time step (stat).
OPTION, BOUNDPDESTROYFQ = 10;    // Delete lost particles, if any out of 10 \sigma
OPTION, AUTOPHASE       = 4;     // Autophase is on, and phase of max energy
                                 // gain will be found automatically for cavities.
OPTION, VERSION=10900;

Title, string="Test simple Gaussian distribution with space charge";

Value,{OPALVERSION};

// ----------------------------------------------------------------------------
// Global Parameters

REAL N                   = 32; 
REAL rf_freq             = 1.3e9;    // RF frequency. (Hz)
REAL n_particles         = N*N*N;      // Number of particles in simulation.
REAL beam_bunch_charge   = 1e-9;     // Charge of bunch. (C)

// ----------------------------------------------------------------------------
// Initial Momentum Calculation

REAL Edes    = 1e-5;   // 100 keV initial kinetic energy
REAL gamma   = (Edes+EMASS)/EMASS; 
REAL beta    = sqrt(1-(1/gamma^2));
REAL P0      = gamma*beta*EMASS;    // initial z momentum

//Printing initial energy and momentum to terminal output.
value, {Edes, P0};

D1: DRIFT, L = 0.25, ELEMEDGE = 0.0;                // drift: particles enter with similar v
E1: CONSTANTEZ, L = 3.75, ELEMEDGE = 0.25, EZ = -1.0;  // -1 MV/m cavity: ~1.75 MeV gain for electrons
myLine: Line = (D1, E1);

BEAM1:  BEAM, PARTICLE = ELECTRON, pc = P0, NPART = n_particles,
        BFREQ = rf_freq, BCURRENT = beam_bunch_charge * rf_freq * 1e6, CHARGE = -1;

BINS1: BINNING, 
    MAXBINS      = 120, 
    DESIREDWIDTH = 0.2, 
    BINNINGALPHA = 1.1, 
    BINNINGBETA  = 1.6, 
    PARAMETER    = VELOCITYZ; // Could be "VELOCITYZ", "POSITIONZ", "PZ" or "GAMMAZ"

FS1: Fieldsolver, TYPE=OPEN, BINS=BINS1,
    NX=N, 
    NY=N, 
    NZ=N,  
    PARFFTX = true, 
    PARFFTY = true, 
    PARFFTZ = false,
    BCFFTX=OPEN, 
    BCFFTY=OPEN, 
    BCFFTZ=OPEN, BBOXINCR = 5, GREENSF = STANDARD;

Dist1: DISTRIBUTION, TYPE=GAUSS, 
    SIGMAX=1e-3, 
    SIGMAY=1e-3, 
    SIGMAZ=0.1,     
    SIGMAPX=1e-9, 
    SIGMAPY=1e-9, 
    SIGMAPZ=1e-2;    // small initial pz spread

TRACK, LINE = myLine, BEAM = BEAM1, MAXSTEPS = 200, DT = {1e-10}, ZSTOP=4.0; 
 RUN, METHOD = "PARALLEL", BEAM = BEAM1, FIELDSOLVER = FS1, DISTRIBUTION = Dist1;ENDTRACK;
QUIT;